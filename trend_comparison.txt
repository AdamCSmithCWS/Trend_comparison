

### model for comparing trend estimates from two (indexed by e) different analyses (or regions), for species (indexed by s)
### for this comparison, e==1 are estimates from BCR 22 and e==2 from BCR 23
### Model is modified from the grassland bird model from Link and Barker 2010, pg 90, also described in Sauer and Link 2002, Ecology 83:1743-1751
### in essence, this is a paired t-test style comparison, that accounts for the imprecision in each s*e trend estimate.

### input data compiled in R, consist of nspecies (the number of species), as well as 3 matrices: varhat, betahat, and n, each of which has nspecies rows and 2 columns
## varhat = estimates of the log-scale variances of trends
## betahat = estimates of the log-scale trends
## nroutes = sample size of the trends - number of routes or survey-sites used to generate the trend)
## nspecies = number of species

model{

for (e in 1:2) {

for(s in 1:nspecies) {

	varhat[s,e] ~ dgamma(p[s,e],lam[s,e]) #varhat = data = variance of trend estimates
	p[s,e] <- nroutes[s,e] / 2
	lam[s,e] <- p[s,e] * tau.betahat[s,e]
	tau.betahat[s,e] ~ dgamma(0.001,0.001)
	sd.betahat[s,e] <- 1/sqrt(tau.betahat[s,e])
	### above is prior for the estimated species level precision (tau-betahat), accounting fo rhte number of routes included
	
	betahat[s,e] ~ dnorm(beta[s,e],tau.betahat[s,e]) #betahat = data = trend estimates
	beta[s,e] ~ dnorm(0.0,1)
	#### beta is the prior for the estimated species level trend, accounting for the precision
	
	 pos[s,e] <- step(beta[s,e]) #tracks whether the trend is positive
	 neg[s,e] <- 1-pos[s,e] #tracks opposite
	

} # end of first s-species loop
  numpos[e] <- sum(pos[,e]) # if pos is tracked, then this estimates the number of species with positive trends
  numneg[e] <- sum(neg[,e]) # if neg is tracked, same

	} #end of e loop (indexing two models being compared)

### species level differences between each trend-source
### if dif[s] is positive = first-source of trend is more positive than second-source of trend
for(s in 1:nspecies) {
dif[s] <- ((exp(beta[s,1])-1)*100)-((exp(beta[s,2])-1)*100) # dif is a vector of the species-specific trend differences (%/year) after accounting for the imprecision of each estimate's trend and the group (survey/monitoring program) structure
difvar[s] <- sd.betahat[s,1]-sd.betahat[s,2] # difvar is a vector of the species-specific differences in sd of the trends (log-scale, not %/year) (not necessarily interesting)
} # end of second s-species loop


m.dif <- mean(dif[])
pos_m.dif <- step(m.dif) #tracks whether the trend is positive (posterior mean of this value is the probability that m.dif is positive)
m.difvar <- mean(difvar[])

dif.numpos <- numpos[1]-numpos[2]
dif.numneg <- numneg[1]-numneg[2]


} # end of model

   